//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument
plugins {
    id 'java-library'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java-library'

    group = 'com.filkond'
    version = '1.0.3'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    if (!project.name.contains('example') && !project.name.contains('plugin')) {
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    from components.java
                    artifactId = project.name.toLowerCase()
                    groupId = project.group.toLowerCase()
                    version = project.version.toLowerCase()
                    pom {
                        developers {
                            developer {
                                id = 'FiLKoNd'
                                email = 'fil.yt.pass@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
        implementation group: 'com.j256.ormlite', name: 'ormlite-jdbc', version: '6.1'

        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/FiLKoNd/UpgradesAPI'
            credentials {
                username = System.getenv("GITHUB_USERNAME")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}